buildscript {
  if (project == rootProject) {
    repositories {
      google()
      mavenCentral()
      jcenter()
    }

    dependencies {
      classpath 'com.android.tools.build:gradle:3.5.3'
    }
  }
}

apply plugin: 'com.android.library'

def safeExtGet(prop, fallback) {
  rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

def isNewArchitectureEnabled() {
  return rootProject.hasProperty("newArchEnabled") && rootProject.getProperty("newArchEnabled") == "true"
}

def reactNativeArchitectures() {
  def value = rootProject.getProperties().get("reactNativeArchitectures")
  return value ? value.split(",") : ["armeabi-v7a", "x86", "x86_64", "arm64-v8a"]
}

android {
  compileSdkVersion safeExtGet('Rtmp_compileSdkVersion', 31)
  
  if (isNewArchitectureEnabled()) {
    buildFeatures {
      prefab true
    }
  }
  
  defaultConfig {
    minSdkVersion safeExtGet('Rtmp_minSdkVersion', 21)
    targetSdkVersion safeExtGet('Rtmp_targetSdkVersion', 31)
    versionCode 1
    versionName "1.0"
    buildConfigField "boolean", "IS_NEW_ARCHITECTURE_ENABLED", isNewArchitectureEnabled().toString()
    
    externalNativeBuild {
      cmake {
        cppFlags "-O2 -frtti -fexceptions -Wall -Wno-unused-variable -fstack-protector-all"
        abiFilters (*reactNativeArchitectures())
        arguments "-DANDROID_STL=c++_shared"
      }
    }
  }

  buildTypes {
    release {
      minifyEnabled false
    }
  }
  lintOptions {
    disable 'GradleCompatible'
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_11
    targetCompatibility JavaVersion.VERSION_11
  }
  
  sourceSets {
    main {
      if (isNewArchitectureEnabled()) {
        java.srcDirs += [
          "build/generated/source/codegen/java"
        ]
      }
    }
  }
}

repositories {
  mavenLocal()
  maven {
    // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
    url("$rootDir/../node_modules/react-native/android")
  }
  google()
  mavenCentral()
  jcenter()
  maven { url 'https://www.jitpack.io' }
}

dependencies {
  //noinspection GradleDynamicVersion
  implementation fileTree(dir: 'libs', include: ['*.jar'])
  implementation 'com.github.pedroSG94.RootEncoder:library:2.6.6'
  implementation "com.facebook.react:react-native:+"  // From node_modules
  
  if (isNewArchitectureEnabled()) {
    implementation project(":ReactAndroid")
  }
}
